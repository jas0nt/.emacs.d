#+STARTUP: overview 
#+PROPERTY: header-args :comments yes :results silent

* Basic
#+begin_src emacs-lisp
  (setq gc-cons-threshold most-positive-fixnum)

  (defvar better-gc-cons-threshold 134217728 ; 128mb
	"The default value to use for `gc-cons-threshold'.

  If you experience freezing, decrease this.  If you experience stuttering, increase this.")

  (add-hook 'emacs-startup-hook
	    (lambda ()
	      (if (boundp 'after-focus-change-function)
		  (add-function :after after-focus-change-function
				(lambda ()
				  (unless (frame-focus-state)
				    (garbage-collect))))
		(add-hook 'after-focus-change-function 'garbage-collect))
	      (defun gc-minibuffer-setup-hook ()
		(setq gc-cons-threshold (* better-gc-cons-threshold 2)))

	      (defun gc-minibuffer-exit-hook ()
		(garbage-collect)
		(setq gc-cons-threshold better-gc-cons-threshold))

	      (add-hook 'minibuffer-setup-hook #'gc-minibuffer-setup-hook)
	      (add-hook 'minibuffer-exit-hook #'gc-minibuffer-exit-hook)))


  (prefer-coding-system 'utf-8)
  (setq locale-coding-system 'utf-8)

  (set-language-environment 'utf-8)
  (set-default-coding-systems 'utf-8)
  (set-buffer-file-coding-system 'utf-8)
  (set-clipboard-coding-system 'utf-8)
  (set-file-name-coding-system 'utf-8)
  (set-keyboard-coding-system 'utf-8)
  (set-terminal-coding-system 'utf-8)
  (set-selection-coding-system 'utf-8)
  (modify-coding-system-alist 'process "*" 'utf-8)

  (when (eq system-type 'darwin)
    (setq mac-option-modifier 'meta)
    (setq mac-command-modifier 'super)
    (setq mac-pass-command-to-system nil))
#+end_src
* Const
#+BEGIN_SRC emacs-lisp
  (defconst my-leader-key "<SPC>")
  (defconst my-note-file "~/mynote/note.org")

  (setq is-on-linux (eq system-type 'gnu/linux))
  (setq my-font (if is-on-linux
		    "Source Code Pro-9"
		  "Source Code Pro-14"))

  (setq my-shell (if is-on-linux
		     "/usr/bin/fish"
		   "/usr/local/bin/fish"))
#+END_SRC
* Packages
#+begin_src emacs-lisp
  (require 'package)
  (setq package-archives '(("gnu"   . "http://elpa.emacs-china.org/gnu/")
			   ("melpa" . "http://elpa.emacs-china.org/melpa/")
			   ("melpa-stable" . "https://stable.melpa.org/packages/")))
  (package-initialize)
  ;; install use-package
  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
    (package-install 'use-package))
  (require 'use-package-ensure)
  (setq use-package-always-ensure t)

  (add-to-list 'load-path
	       (expand-file-name (concat user-emacs-directory "elisp")))
#+end_src
** ace-pinyin
#+BEGIN_SRC emacs-lisp
  (use-package ace-pinyin
    :config
    (ace-pinyin-global-mode +1))
#+END_SRC
** ace-window
#+BEGIN_SRC emacs-lisp
  (use-package ace-window
    :init
    (progn
      (global-set-key [remap other-window] 'ace-window)
      (setq aw-keys '(?a ?s ?d ?f ?g ?h ?j ?k ?l))
      (custom-set-faces
       '(aw-leading-char-face
	 ((t (:inhrit ace-jump-face-foreground :height 3.0)))))))
#+END_SRC
** all-the-icons
#+BEGIN_SRC emacs-lisp
  (use-package all-the-icons)
#+END_SRC
** beacon
#+BEGIN_SRC emacs-lisp
  (use-package beacon
    :config
    (beacon-mode 1)
    (setq beacon-color "#00FF00"))
#+END_SRC
** benchmark-init
#+BEGIN_SRC emacs-lisp
  (use-package benchmark-init
    :init (benchmark-init/activate)
    :hook (after-init . benchmark-init/deactivate))
#+END_SRC
** bongo
#+BEGIN_SRC emacs-lisp
  (use-package bongo
    ;; :if (eq system-type 'gnu/linux)
    :config
    (setq bongo-logo nil)
    (setq bongo-display-track-icons nil)
    (setq bongo-display-track-lengths nil)
    (setq bongo-display-header-icons nil)
    (setq bongo-display-playback-mode-indicator t)
    (setq bongo-header-line-mode nil)
    (setq bongo-mode-line-indicator-mode nil)
    (setq bongo-field-separator (propertize " · " 'face 'shadow))

    (setq bongo-prefer-library-buffers nil)
    (setq bongo-insert-whole-directory-trees t)
    ;;(setq bongo-join-inserted-tracks nil)
    (setq bongo-enabled-backends '(vlc))

    (defun init-goto-bongo ()
      (interactive)
      (let ((bongo-playlist-buffer-name "*Bongo Playlist*"))
	(unless (get-buffer bongo-playlist-buffer-name)
	  (bongo)
	  (bongo-insert-directory-tree "~/Music/my_music")
	  (goto-char (point-min))
	  (bongo-random-playback-mode))
	(switch-to-buffer bongo-playlist-buffer-name)))

    (defhydra hydra-bongo ()
      ("m" init-goto-bongo "goto-bongo" :color blue)
      ("s" bongo-pause/resume "play/pause")
      ("r" bongo-play-random "play random")
      ("n" bongo-play-next "play next")
      ("p" bongo-play-previous "play previous")
      ("f" bongo-seek-forward-10 "forward")
      ("F" bongo-seek-forward-60 "Forward")
      ("b" bongo-seek-backward-10 "backward")
      ("B" bongo-seek-backward-60 "Backward")
      ("q" nil "Cancel")))
#+END_SRC
** bufler
#+BEGIN_SRC emacs-lisp
  (use-package bufler)
#+END_SRC
** company
#+BEGIN_SRC emacs-lisp
  (use-package company
    :diminish (company-mode " Cmp.")
    :defines (company-dabbrev-ignore-case company-dabbrev-downcase)
    :hook (after-init . global-company-mode)
    :config (setq company-dabbrev-code-everywhere t
		  company-dabbrev-code-modes t
		  company-dabbrev-code-other-buffers 'all
		  company-dabbrev-downcase nil
		  company-dabbrev-ignore-case t
		  company-dabbrev-other-buffers 'all
		  company-require-match nil
		  company-minimum-prefix-length 1
		  company-show-numbers t
		  company-tooltip-limit 20
		  company-idle-delay 0
		  company-echo-delay 0
		  company-tooltip-offset-display 'scrollbar
		  company-begin-commands '(self-insert-command))
    (eval-after-load 'company
      '(add-to-list 'company-backends
		    '(company-abbrev company-yasnippet company-capf))))

  (use-package company-prescient
    :init (company-prescient-mode 1))

  (use-package company-box
    :diminish
    :defines company-box-icons-all-the-icons
    :hook (company-mode . company-box-mode)
    :init (setq company-box-backends-colors nil
		company-box-doc-delay 0.1)
    :config
    (with-no-warnings
      ;; Prettify icons
      (defun my-company-box-icons--elisp (candidate)
	(when (or (derived-mode-p 'emacs-lisp-mode) (derived-mode-p 'lisp-mode))
	  (let ((sym (intern candidate)))
	    (cond ((fboundp sym) 'Function)
		  ((featurep sym) 'Module)
		  ((facep sym) 'Color)
		  ((boundp sym) 'Variable)
		  ((symbolp sym) 'Text)
		  (t . nil)))))
      (advice-add #'company-box-icons--elisp :override #'my-company-box-icons--elisp)

      ;; Display borders and optimize performance
      (defun my-company-box--display (string on-update)
	"Display the completions."
	(company-box--render-buffer string on-update)

	(let ((frame (company-box--get-frame))
	      (border-color (face-foreground 'font-lock-comment-face nil t)))
	  (unless frame
	    (setq frame (company-box--make-frame))
	    (company-box--set-frame frame))
	  (company-box--compute-frame-position frame)
	  (company-box--move-selection t)
	  (company-box--update-frame-position frame)
	  (unless (frame-visible-p frame)
	    (make-frame-visible frame))
	  (company-box--update-scrollbar frame t)
	  (set-face-background 'internal-border border-color frame)
	  (when (facep 'child-frame-border)
	    (set-face-background 'child-frame-border border-color frame)))
	(with-current-buffer (company-box--get-buffer)
	  (company-box--maybe-move-number (or company-box--last-start 1))))
      (advice-add #'company-box--display :override #'my-company-box--display)

      (setq company-box-doc-frame-parameters '((internal-border-width . 1)
					       (left-fringe . 8)
					       (right-fringe . 8)))

      (defun my-company-box-doc--make-buffer (object)
	(let* ((buffer-list-update-hook nil)
	       (inhibit-modification-hooks t)
	       (string (cond ((stringp object) object)
			     ((bufferp object) (with-current-buffer object (buffer-string))))))
	  (when (and string (> (length (string-trim string)) 0))
	    (with-current-buffer (company-box--get-buffer "doc")
	      (erase-buffer)
	      (insert (propertize "\n" 'face '(:height 0.5)))
	      (insert string)
	      (insert (propertize "\n\n" 'face '(:height 0.5)))

	      ;; Handle hr lines of markdown
	      ;; @see `lsp-ui-doc--handle-hr-lines'
	      (with-current-buffer (company-box--get-buffer "doc")
		(let (bolp next before after)
		  (goto-char 1)
		  (while (setq next (next-single-property-change (or next 1) 'markdown-hr))
		    (when (get-text-property next 'markdown-hr)
		      (goto-char next)
		      (setq bolp (bolp)
			    before (char-before))
		      (delete-region (point) (save-excursion (forward-visible-line 1) (point)))
		      (setq after (char-after (1+ (point))))
		      (insert
		       (concat
			(and bolp (not (equal before ?\n)) (propertize "\n" 'face '(:height 0.5)))
			(propertize "\n" 'face '(:height 0.5))
			(propertize " "
				    'display '(space :height (1))
				    'company-box-doc--replace-hr t
				    'face `(:background ,(face-foreground 'font-lock-comment-face)))
			(propertize " " 'display '(space :height (1)))
			(and (not (equal after ?\n)) (propertize " \n" 'face '(:height 0.5)))))))))

	      (setq mode-line-format nil
		    display-line-numbers nil
		    header-line-format nil
		    show-trailing-whitespace nil
		    cursor-in-non-selected-windows nil)
	      (current-buffer)))))
      (advice-add #'company-box-doc--make-buffer :override #'my-company-box-doc--make-buffer)

      ;; Display the border and fix the markdown header properties
      (defun my-company-box-doc--show (selection frame)
	(cl-letf (((symbol-function 'completing-read) #'company-box-completing-read)
		  (window-configuration-change-hook nil)
		  (inhibit-redisplay t)
		  (display-buffer-alist nil)
		  (buffer-list-update-hook nil))
	  (-when-let* ((valid-state (and (eq (selected-frame) frame)
					 company-box--bottom
					 company-selection
					 (company-box--get-frame)
					 (frame-visible-p (company-box--get-frame))))
		       (candidate (nth selection company-candidates))
		       (doc (or (company-call-backend 'quickhelp-string candidate)
				(company-box-doc--fetch-doc-buffer candidate)))
		       (doc (company-box-doc--make-buffer doc)))
	    (let ((frame (frame-local-getq company-box-doc-frame))
		  (border-color (face-foreground 'font-lock-comment-face nil t)))
	      (unless (frame-live-p frame)
		(setq frame (company-box-doc--make-frame doc))
		(frame-local-setq company-box-doc-frame frame))
	      (set-face-background 'internal-border border-color frame)
	      (when (facep 'child-frame-border)
		(set-face-background 'child-frame-border border-color frame))
	      (company-box-doc--set-frame-position frame)

	      ;; Fix hr props. @see `lsp-ui-doc--fix-hr-props'
	      (with-current-buffer (company-box--get-buffer "doc")
		(let (next)
		  (while (setq next (next-single-property-change (or next 1) 'company-box-doc--replace-hr))
		    (when (get-text-property next 'company-box-doc--replace-hr)
		      (put-text-property next (1+ next) 'display
					 '(space :align-to (- right-fringe 1) :height (1)))
		      (put-text-property (1+ next) (+ next 2) 'display
					 '(space :align-to right-fringe :height (1)))))))

	      (unless (frame-visible-p frame)
		(make-frame-visible frame))))))
      (advice-add #'company-box-doc--show :override #'my-company-box-doc--show)

      (defun my-company-box-doc--set-frame-position (frame)
	(-let* ((frame-resize-pixelwise t)

		(box-frame (company-box--get-frame))
		(box-position (frame-position box-frame))
		(box-width (frame-pixel-width box-frame))
		(box-height (frame-pixel-height box-frame))
		(box-border-width (frame-border-width box-frame))

		(window (frame-root-window frame))
		((text-width . text-height) (window-text-pixel-size window nil nil
								    (/ (frame-pixel-width) 2)
								    (/ (frame-pixel-height) 2)))
		(border-width (or (alist-get 'internal-border-width company-box-doc-frame-parameters) 0))

		(x (- (+ (car box-position) box-width) border-width))
		(space-right (- (frame-pixel-width) x))
		(space-left (car box-position))
		(fringe-left (or (alist-get 'left-fringe company-box-doc-frame-parameters) 0))
		(fringe-right (or (alist-get 'right-fringe company-box-doc-frame-parameters) 0))
		(width (+ text-width border-width fringe-left fringe-right))
		(x (if (> width space-right)
		       (if (> space-left width)
			   (- space-left width)
			 space-left)
		     x))
		(y (cdr box-position))
		(bottom (+ company-box--bottom (frame-border-width)))
		(height (+ text-height (* 2 border-width)))
		(y (cond ((= x space-left)
			  (if (> (+ y box-height height) bottom)
			      (+ (- y height) border-width)
			    (- (+ y box-height) border-width)))
			 ((> (+ y height) bottom)
			  (- (+ y box-height) height))
			 (t y))))
	  (set-frame-position frame (max x 0) (max y 0))
	  (set-frame-size frame text-width text-height t)))
      (advice-add #'company-box-doc--set-frame-position :override #'my-company-box-doc--set-frame-position)

      (setq company-box-icons-all-the-icons
	    `((Unknown . ,(all-the-icons-material "find_in_page" :height 1.0 :v-adjust -0.2))
	      (Text . ,(all-the-icons-faicon "text-width" :height 1.0 :v-adjust -0.02))
	      (Method . ,(all-the-icons-faicon "cube" :height 1.0 :v-adjust -0.02 :face 'all-the-icons-purple))
	      (Function . ,(all-the-icons-faicon "cube" :height 1.0 :v-adjust -0.02 :face 'all-the-icons-purple))
	      (Constructor . ,(all-the-icons-faicon "cube" :height 1.0 :v-adjust -0.02 :face 'all-the-icons-purple))
	      (Field . ,(all-the-icons-octicon "tag" :height 1.1 :v-adjust 0 :face 'all-the-icons-lblue))
	      (Variable . ,(all-the-icons-octicon "tag" :height 1.1 :v-adjust 0 :face 'all-the-icons-lblue))
	      (Class . ,(all-the-icons-material "settings_input_component" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-orange))
	      (Interface . ,(all-the-icons-material "share" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-lblue))
	      (Module . ,(all-the-icons-material "view_module" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-lblue))
	      (Property . ,(all-the-icons-faicon "wrench" :height 1.0 :v-adjust -0.02))
	      (Unit . ,(all-the-icons-material "settings_system_daydream" :height 1.0 :v-adjust -0.2))
	      (Value . ,(all-the-icons-material "format_align_right" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-lblue))
	      (Enum . ,(all-the-icons-material "storage" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-orange))
	      (Keyword . ,(all-the-icons-material "filter_center_focus" :height 1.0 :v-adjust -0.2))
	      (Snippet . ,(all-the-icons-material "format_align_center" :height 1.0 :v-adjust -0.2))
	      (Color . ,(all-the-icons-material "palette" :height 1.0 :v-adjust -0.2))
	      (File . ,(all-the-icons-faicon "file-o" :height 1.0 :v-adjust -0.02))
	      (Reference . ,(all-the-icons-material "collections_bookmark" :height 1.0 :v-adjust -0.2))
	      (Folder . ,(all-the-icons-faicon "folder-open" :height 1.0 :v-adjust -0.02))
	      (EnumMember . ,(all-the-icons-material "format_align_right" :height 1.0 :v-adjust -0.2))
	      (Constant . ,(all-the-icons-faicon "square-o" :height 1.0 :v-adjust -0.1))
	      (Struct . ,(all-the-icons-material "settings_input_component" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-orange))
	      (Event . ,(all-the-icons-octicon "zap" :height 1.0 :v-adjust 0 :face 'all-the-icons-orange))
	      (Operator . ,(all-the-icons-material "control_point" :height 1.0 :v-adjust -0.2))
	      (TypeParameter . ,(all-the-icons-faicon "arrows" :height 1.0 :v-adjust -0.02))
	      (Template . ,(all-the-icons-material "format_align_left" :height 1.0 :v-adjust -0.2)))
	    company-box-icons-alist 'company-box-icons-all-the-icons)))

  (use-package company-quickhelp-terminal
    :defines company-quickhelp-delay
    :bind (:map company-active-map
		([remap company-show-doc-buffer] . company-quickhelp-manual-begin))
    :hook ((global-company-mode . company-quickhelp-mode)
	   (company-quickhelp-mode  . company-quickhelp-terminal-mode))
    :init (setq company-quickhelp-delay 0.3))

  (use-package company-english-helper
    :load-path "~/.emacs.d/elisp/company-english-helper")

  ;; (use-package company-tabnine
  ;;   :config
  ;;   (add-to-list 'company-backends #'company-tabnine))
#+END_SRC
** dired
#+BEGIN_SRC emacs-lisp
  (when (string= system-type "darwin")
    (require 'dired-x)
    (setq dired-guess-shell-alist-user '(("\\.*\\'" "open")))
    (setq dired-use-ls-dired nil))

  (setq dired-listing-switches "-alht")
#+END_SRC
** discover-my-major
#+begin_src emacs-lisp
  (use-package discover-my-major
    :bind ("C-h C-m" . discover-my-major))
#+end_src
** evil
#+BEGIN_SRC emacs-lisp
  (use-package evil
    :init
    (setq evil-want-integration t) ;; This is optional since it's already set to t by default.
    (setq evil-want-keybinding nil)
    (setq evil-disable-insert-state-bindings t)
    (setq evil-want-C-i-jump nil)
    (setq evil-want-C-u-scroll t)
    :config
    (evil-mode 1)
    (setq evil-insert-state-cursor '(hollow "yellow")
	  evil-normal-state-cursor '(box "green")))

  (use-package evil-collection
    :after (evil)
    :init
    (setq evil-collection-company-use-tng nil)
    :config
    (evil-collection-init))

  (use-package evil-nerd-commenter)

  (use-package evil-pinyin
    :after (evil)
    :init
    (setq-default evil-pinyin-scheme 'simplified-xiaohe-all)
    (setq-default evil-pinyin-with-search-rule 'always)
    :config
    (evil-select-search-module 'evil-search-module 'evil-search)
    (global-evil-pinyin-mode))

  (use-package evil-exchange
    :config
    (evil-exchange-install))
#+END_SRC
** expand-region
#+BEGIN_SRC emacs-lisp
  (use-package expand-region)
#+END_SRC
** find-file-in-project
#+begin_src emacs-lisp
 (use-package find-file-in-project
   :config
   (ivy-mode 1))
#+end_src
** general
#+BEGIN_SRC emacs-lisp
  (use-package general)
#+END_SRC
** google-this
#+BEGIN_SRC emacs-lisp
  (use-package google-this)
#+END_SRC
** hungry-delete & aggresive-indent
#+BEGIN_SRC emacs-lisp
  (use-package hungry-delete
    :config
    (global-hungry-delete-mode))
  (use-package aggressive-indent
    :config
    (global-aggressive-indent-mode 1))
#+END_SRC
** hydra
#+BEGIN_SRC emacs-lisp
  (use-package hydra :ensure hydra)
#+END_SRC
** ialign
#+BEGIN_SRC emacs-lisp
  (use-package ialign)
#+END_SRC
** ivy
#+BEGIN_SRC emacs-lisp
  (use-package counsel
    :init
    (setq enable-recursive-minibuffers t) ; Allow commands in minibuffers

    (setq ivy-height 12
	  ivy-use-selectable-prompt t
	  ivy-use-virtual-buffers t    ; Enable bookmarks and recentf
	  ivy-fixed-height-minibuffer t
	  ivy-count-format "(%d/%d) "
	  ivy-ignore-buffers '("\\` " "\\`\\*tramp/" "\\`\\*xref" "\\`\\*helpful "
			       "\\`\\*.+-posframe-buffer\\*")
	  ivy-on-del-error-function #'ignore
	  ivy-initial-inputs-alist nil)

    (setq swiper-action-recenter t)

    (setq counsel-find-file-at-point t
	  counsel-preselect-current-file t
	  counsel-yank-pop-separator "\n────────\n")
    (add-hook 'counsel-grep-post-action-hook #'recenter)

    ;; Use the faster search tools
    (when (executable-find "rg")
      (setq counsel-grep-base-command "rg -S --no-heading --line-number --color never '%s' '%s'"))
    (when (executable-find "fd")
      (setq counsel-fzf-cmd
	    "fd --type f --hidden --follow --exclude .git --color never '%s'")))

  (use-package ivy
    :custom-face
    (ivy-current-match ((t (:extend t :background "yellow" :foreground "#ff79c6" :weight bold)))))

  (use-package ivy-posframe
    :init
    (setq ivy-posframe-display-functions-alist '((t . ivy-posframe-display)))
    :config
    (ivy-posframe-mode 1))

  (use-package ivy-rich
    :hook ((counsel-projectile-mode . ivy-rich-mode) ; MUST after `counsel-projectile'
	   (ivy-rich-mode . ivy-rich-project-root-cache-mode)
	   (ivy-rich-mode . (lambda ()
			      "Use abbreviate in `ivy-rich-mode'."
			      (setq ivy-virtual-abbreviate
				    (or (and ivy-rich-mode 'abbreviate) 'name)))))
    :init
    ;; For better performance
    (setq ivy-rich-parse-remote-buffer nil))

  (use-package prescient
    :commands prescient-persist-mode
    :init (prescient-persist-mode 1))

  (use-package ivy-prescient
    :commands ivy-prescient-re-builder
    :custom-face
    (ivy-minibuffer-match-face-1 ((t (:foreground ,(face-foreground 'font-lock-doc-face nil t)))))
    :init
    (defun ivy-prescient-non-fuzzy (str)
      "Generate an Ivy-formatted non-fuzzy regexp list for the given STR.
  This is for use in `ivy-re-builders-alist'."
      (let ((prescient-filter-method '(literal regexp)))
	(ivy-prescient-re-builder str)))

    (setq ivy-prescient-retain-classic-highlighting t
	  ivy-re-builders-alist
	  '((counsel-ag . ivy-prescient-non-fuzzy)
	    (counsel-rg . ivy-prescient-non-fuzzy)
	    (counsel-pt . ivy-prescient-non-fuzzy)
	    (counsel-grep . ivy-prescient-non-fuzzy)
	    (counsel-fzf . ivy-prescient-non-fuzzy)
	    (counsel-imenu . ivy-prescient-non-fuzzy)
	    (counsel-yank-pop . ivy-prescient-non-fuzzy)
	    (swiper . ivy-prescient-non-fuzzy)
	    (swiper-isearch . ivy-prescient-non-fuzzy)
	    (swiper-all . ivy-prescient-non-fuzzy)
	    (lsp-ivy-workspace-symbol . ivy-prescient-non-fuzzy)
	    (lsp-ivy-global-workspace-symbol . ivy-prescient-non-fuzzy)
	    (insert-char . ivy-prescient-non-fuzzy)
	    (counsel-unicode-char . ivy-prescient-non-fuzzy)
	    (t . ivy-prescient-re-builder))
	  ivy-prescient-sort-commands
	  '(:not swiper swiper-isearch ivy-switch-buffer
		 lsp-ivy-workspace-symbol ivy-resume ivy--restore-session
		 counsel-grep counsel-git-grep counsel-rg counsel-ag
		 counsel-ack counsel-fzf counsel-pt counsel-imenu
		 counsel-org-capture counsel-outline counsel-org-goto
		 counsel-load-theme counsel-yank-pop
		 counsel-recentf counsel-buffer-or-recentf))

    (ivy-prescient-mode 1))
#+END_SRC
** lsp
#+BEGIN_SRC emacs-lisp
  (use-package lsp-mode
    :diminish
    :defines lsp-clients-python-library-directories
    :commands (lsp-enable-which-key-integration
	       lsp-format-buffer
	       lsp-organize-imports
	       lsp-install-server)
    :custom-face
    (lsp-headerline-breadcrumb-path-error-face
     ((t :underline (:style wave :color ,(face-foreground 'error))
	 :inherit lsp-headerline-breadcrumb-path-face)))
    (lsp-headerline-breadcrumb-path-warning-face
     ((t :underline (:style wave :color ,(face-foreground 'warning))
	 :inherit lsp-headerline-breadcrumb-path-face)))
    (lsp-headerline-breadcrumb-path-info-face
     ((t :underline (:style wave :color ,(face-foreground 'success))
	 :inherit lsp-headerline-breadcrumb-path-face)))
    (lsp-headerline-breadcrumb-path-hint-face
     ((t :underline (:style wave :color ,(face-foreground 'success))
	 :inherit lsp-headerline-breadcrumb-path-face)))

    (lsp-headerline-breadcrumb-symbols-error-face
     ((t :inherit lsp-headerline-breadcrumb-symbols-face
	 :underline (:style wave :color ,(face-foreground 'error)))))
    (lsp-headerline-breadcrumb-symbols-warning-face
     ((t :inherit lsp-headerline-breadcrumb-symbols-face
	 :underline (:style wave :color ,(face-foreground 'warning)))))
    (lsp-headerline-breadcrumb-symbols-info-face
     ((t :inherit lsp-headerline-breadcrumb-symbols-face
	 :underline (:style wave :color ,(face-foreground 'success)))))
    (lsp-headerline-breadcrumb-symbols-hint-face
     ((t :inherit lsp-headerline-breadcrumb-symbols-face
	 :underline (:style wave :color ,(face-foreground 'success)))))
    :hook ((prog-mode . (lambda ()
			  (unless (derived-mode-p 'emacs-lisp-mode 'lisp-mode 'makefile-mode)
			    (lsp-deferred))))
	   (lsp-mode . (lambda ()
			 ;; Integrate `which-key'
			 (lsp-enable-which-key-integration)
			 )))
    :bind (:map lsp-mode-map
		("C-c C-d" . lsp-describe-thing-at-point)
		([remap xref-find-definitions] . lsp-find-definition)
		([remap xref-find-references] . lsp-find-references))
    :init
    ;; @see https://emacs-lsp.github.io/lsp-mode/page/performance
    (setq read-process-output-max (* 1024 1024)) ;; 1MB

    (setq lsp-keymap-prefix "C-c l"
	  lsp-keep-workspace-alive nil
	  lsp-signature-auto-activate nil
	  lsp-modeline-code-actions-enable nil
	  lsp-modeline-diagnostics-enable nil
	  lsp-modeline-workspace-status-enable nil

	  lsp-enable-file-watchers nil
	  lsp-enable-folding nil
	  lsp-enable-symbol-highlighting nil
	  lsp-enable-text-document-color nil

	  lsp-enable-indentation nil
	  lsp-enable-on-type-formatting nil)

    ;; For `lsp-clients'
    (setq lsp-clients-python-library-directories '("/usr/local/" "/usr/"))
    :config
    (with-no-warnings
      ;; Disable `lsp-mode' in `git-timemachine-mode'
      (defun my-lsp--init-if-visible (fn &rest args)
	(unless (bound-and-true-p git-timemachine-mode)
	  (apply fn args)))
      (advice-add #'lsp--init-if-visible :around #'my-lsp--init-if-visible)

      ;; Enable `lsp-mode' in sh/bash/zsh
      (defun my-lsp-bash-check-sh-shell (&rest _)
	(and (eq major-mode 'sh-mode)
	     (memq sh-shell '(sh bash zsh))))
      (advice-add #'lsp-bash-check-sh-shell :override #'my-lsp-bash-check-sh-shell)

      ;; Only display icons in GUI
      (defun my-lsp-icons-get-symbol-kind (fn &rest args)
	(when (display-graphic-p)
	  (apply fn args)))
      (advice-add #'lsp-icons-get-by-symbol-kind :around #'my-lsp-icons-get-symbol-kind)

      (defun my-lsp-icons-get-by-file-ext (fn &rest args)
	(when (display-graphic-p)
	  (apply fn args)))
      (advice-add #'lsp-icons-get-by-file-ext :around #'my-lsp-icons-get-by-file-ext)

      (defun my-lsp-icons-all-the-icons-material-icon (icon-name face fallback &optional feature)
	(if (and (display-graphic-p)
		 (functionp 'all-the-icons-material)
		 (lsp-icons--enabled-for-feature feature))
	    (all-the-icons-material icon-name
				    :face face)
	  (propertize fallback 'face face)))
      (advice-add #'lsp-icons-all-the-icons-material-icon
		  :override #'my-lsp-icons-all-the-icons-material-icon))

    (defun lsp-update-server ()
      "Update LSP server."
      (interactive)
      ;; Equals to `C-u M-x lsp-install-server'
      (lsp-install-server t)))

  (use-package lsp-ui
    :custom-face
    (lsp-ui-sideline-code-action ((t (:inherit warning))))
    :pretty-hydra
    ((:title (pretty-hydra-title "LSP UI" 'faicon "rocket" :face 'all-the-icons-green)
	     :color amaranth :quit-key "q")
     ("Doc"
      (("d e" (progn
		(lsp-ui-doc-enable (not lsp-ui-doc-mode))
		(setq lsp-ui-doc-enable (not lsp-ui-doc-enable)))
	"enable" :toggle lsp-ui-doc-mode)
       ("d s" (setq lsp-ui-doc-include-signature (not lsp-ui-doc-include-signature))
	"signature" :toggle lsp-ui-doc-include-signature)
       ("d t" (setq lsp-ui-doc-position 'top)
	"top" :toggle (eq lsp-ui-doc-position 'top))
       ("d b" (setq lsp-ui-doc-position 'bottom)
	"bottom" :toggle (eq lsp-ui-doc-position 'bottom))
       ("d p" (setq lsp-ui-doc-position 'at-point)
	"at point" :toggle (eq lsp-ui-doc-position 'at-point))
       ("d h" (setq lsp-ui-doc-header (not lsp-ui-doc-header))
	"header" :toggle lsp-ui-doc-header)
       ("d f" (setq lsp-ui-doc-alignment 'frame)
	"align frame" :toggle (eq lsp-ui-doc-alignment 'frame))
       ("d w" (setq lsp-ui-doc-alignment 'window)
	"align window" :toggle (eq lsp-ui-doc-alignment 'window)))
      "Sideline"
      (("s e" (progn
		(lsp-ui-sideline-enable (not lsp-ui-sideline-mode))
		(setq lsp-ui-sideline-enable (not lsp-ui-sideline-enable)))
	"enable" :toggle lsp-ui-sideline-mode)
       ("s h" (setq lsp-ui-sideline-show-hover (not lsp-ui-sideline-show-hover))
	"hover" :toggle lsp-ui-sideline-show-hover)
       ("s d" (setq lsp-ui-sideline-show-diagnostics (not lsp-ui-sideline-show-diagnostics))
	"diagnostics" :toggle lsp-ui-sideline-show-diagnostics)
       ("s s" (setq lsp-ui-sideline-show-symbol (not lsp-ui-sideline-show-symbol))
	"symbol" :toggle lsp-ui-sideline-show-symbol)
       ("s c" (setq lsp-ui-sideline-show-code-actions (not lsp-ui-sideline-show-code-actions))
	"code actions" :toggle lsp-ui-sideline-show-code-actions)
       ("s i" (setq lsp-ui-sideline-ignore-duplicate (not lsp-ui-sideline-ignore-duplicate))
	"ignore duplicate" :toggle lsp-ui-sideline-ignore-duplicate))
      "Action"
      (("h" backward-char "←")
       ("j" next-line "↓")
       ("k" previous-line "↑")
       ("l" forward-char "→")
       ("C-a" mwim-beginning-of-code-or-line nil)
       ("C-e" mwim-end-of-code-or-line nil)
       ("C-b" backward-char nil)
       ("C-n" next-line nil)
       ("C-p" previous-line nil)
       ("C-f" forward-char nil)
       ("M-b" backward-word nil)
       ("M-f" forward-word nil)
       ("c" lsp-ui-sideline-apply-code-actions "apply code actions"))))
    :bind (("C-c u" . lsp-ui-imenu)
	   :map lsp-ui-mode-map
	   ("M-<f6>" . lsp-ui-hydra/body)
	   ("M-RET" . lsp-ui-sideline-apply-code-actions)
	   ([remap xref-find-definitions] . lsp-ui-peek-find-definitions)
	   ([remap xref-find-references] . lsp-ui-peek-find-references))
    :hook (lsp-mode . lsp-ui-mode)
    :init (setq lsp-ui-sideline-show-diagnostics nil
		lsp-ui-sideline-ignore-duplicate t
		lsp-ui-doc-delay 0.1
		lsp-ui-doc-position 'at-point
		lsp-ui-doc-border (face-foreground 'font-lock-comment-face nil t)
		lsp-ui-imenu-colors `(,(face-foreground 'font-lock-keyword-face)
				      ,(face-foreground 'font-lock-string-face)
				      ,(face-foreground 'font-lock-constant-face)
				      ,(face-foreground 'font-lock-variable-name-face)))
    :config
    (with-no-warnings
      (defun my-lsp-ui-doc--handle-hr-lines nil
	(let (bolp next before after)
	  (goto-char 1)
	  (while (setq next (next-single-property-change (or next 1) 'markdown-hr))
	    (when (get-text-property next 'markdown-hr)
	      (goto-char next)
	      (setq bolp (bolp)
		    before (char-before))
	      (delete-region (point) (save-excursion (forward-visible-line 1) (point)))
	      (setq after (char-after (1+ (point))))
	      (insert
	       (concat
		(and bolp (not (equal before ?\n)) (propertize "\n" 'face '(:height 0.5)))
		(propertize "\n" 'face '(:height 0.5))
		(propertize " "
			    ;; :align-to is added with lsp-ui-doc--fix-hr-props
			    'display '(space :height (1))
			    'lsp-ui-doc--replace-hr t
			    'face `(:background ,(face-foreground 'font-lock-comment-face)))
		;; :align-to is added here too
		(propertize " " 'display '(space :height (1)))
		(and (not (equal after ?\n)) (propertize " \n" 'face '(:height 0.5)))))))))
      (advice-add #'lsp-ui-doc--handle-hr-lines :override #'my-lsp-ui-doc--handle-hr-lines))

    ;; `C-g'to close doc
    (advice-add #'keyboard-quit :before #'lsp-ui-doc-hide)

    ;; Reset `lsp-ui-doc-background' after loading theme
    (add-hook 'after-load-theme-hook
	      (lambda ()
		(setq lsp-ui-doc-border (face-foreground 'font-lock-comment-face nil t))
		(set-face-background 'lsp-ui-doc-background (face-background 'tooltip nil t)))))
#+END_SRC
** magit
#+BEGIN_SRC emacs-lisp
  (use-package magit)

  (use-package git-messenger
    :init (setq git-messenger:show-detail t
		git-messenger:use-magit-popup t))

  (use-package git-timemachine)

  (use-package diff-hl
    :config
    (global-diff-hl-mode)
    (defhydra hydra-diff-hl ()
      "git diff"
      ("j" diff-hl-next-hunk)
      ("k" diff-hl-previous-hunk)
      ("x" diff-hl-revert-hunk)
      ("q" nil "cancel")))

#+END_SRC
** markdown
#+BEGIN_SRC emacs-lisp
  (use-package markdown-preview-eww)
  (use-package markdown-mode
    :commands (markdown-mode gfm-mode)
    :mode (("README\\.md\\'" . gfm-mode)
	   ("\\.md\\'" . markdown-mode)
	   ("\\.markdown\\'" . markdown-mode))
    :init (setq markdown-command "multimarkdown"))
#+END_SRC
** org
#+BEGIN_SRC emacs-lisp
  (use-package org
    :config
    (setq-default prettify-symbols-alist '(("#+BEGIN_SRC" . "✎")
					   ("#+END_SRC" . "□")
					   ("#+begin_src" . "✎")
					   ("#+end_src" . "□")))
    (add-hook 'org-mode-hook 'prettify-symbols-mode)

    (setq org-startup-with-inline-images t)
    (setq org-babel-python-command "python3")
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((python . t)
       (R . t)
       (sql . t)
       )))
#+END_SRC
** popwin
#+BEGIN_SRC emacs-lisp
  (use-package popwin
    :config
    (popwin-mode t))
#+END_SRC
** projectile
#+BEGIN_SRC emacs-lisp
  (use-package projectile
    :config
    (projectile-global-mode)
    (setq projectile-completion-system 'ivy))
#+END_SRC
** python
#+BEGIN_SRC emacs-lisp
  (use-package python-mode
    :config
    (setq python-shell-interpreter "python3"))

  (use-package pyvenv
    :config
    (pyvenv-mode 1))

  (use-package lsp-pyright
    :hook (python-mode . (lambda ()
			   (require 'lsp-pyright)
			   (lsp)))
    :init
    (when (executable-find "python3")
      (setq lsp-pyright-python-executable-cmd "python3")))
#+END_SRC
** rainbow-delimiters
#+BEGIN_SRC emacs-lisp
  (use-package rainbow-delimiters
    :config
    (rainbow-delimiters-mode)
    (add-hook 'prog-mode-hook #'rainbow-delimiters-mode))
#+END_SRC
** restart-emacs
#+BEGIN_SRC emacs-lisp
  (use-package restart-emacs)
#+END_SRC
** restclient
#+BEGIN_SRC emacs-lisp
  (use-package restclient
    :mode ("\\.http\\'" . restclient-mode))
  (use-package company-restclient
    :config
    (add-to-list 'company-backends 'company-restclient))
#+END_SRC
** rime
#+BEGIN_SRC emacs-lisp
  (use-package rime
    :config
    (unless is-on-linux
      (setq rime-librime-root "~/.emacs.d/librime/dist"))
    (setq rime-posframe-properties
	  (list :background-color "#282a36"
		:foreground-color "#bd93f9"
		:font my-font
		:internal-border-width 10))

    (setq default-input-method "rime"
	  rime-show-candidate 'minibuffer))
#+END_SRC
** term
#+begin_src emacs-lisp
  (add-hook 'term-mode-hook (lambda ()
			      (setq-local global-hl-line-mode nil)
			      (setq-local mode-line-format nil)))

  (defun my-new-term ()
    (interactive)
    (ansi-term "/usr/local/bin/fish"))
#+end_src
** try
#+BEGIN_SRC emacs-lisp
  (use-package try)
#+END_SRC
** undo-tree
#+BEGIN_SRC emacs-lisp
(use-package undo-tree
  :init
  (global-undo-tree-mode))
#+END_SRC
** vterm
#+BEGIN_SRC emacs-lisp
  (use-package vterm
    :commands vterm--internal
    :init
    (setq vterm-always-compile-module t)

    (with-no-warnings
      (defvar vterm-posframe--frame nil)

      (defun vterm-posframe-hidehandler (_)
	"Hidehandler used by `vterm-posframe-toggle'."
	(not (eq (selected-frame) posframe--frame)))

      (defun vterm-posframe-toggle ()
	"Toggle `vterm' child frame."
	(interactive)
	(let ((buffer (vterm--internal #'ignore 100)))
	  (if (and vterm-posframe--frame
		   (frame-live-p vterm-posframe--frame)
		   (frame-visible-p vterm-posframe--frame))
	      (progn
		(posframe-hide buffer)
		;; Focus the parent frame
		(select-frame-set-input-focus (frame-parent vterm-posframe--frame)))
	    (let ((width  (max 80 (/ (frame-width) 2)))
		  (height (/ (frame-height) 2)))
	      (setq vterm-posframe--frame
		    (posframe-show
		     buffer
		     :poshandler #'posframe-poshandler-frame-center
		     :hidehandler #'vterm-posframe-hidehandler
		     :left-fringe 8
		     :right-fringe 8
		     :width width
		     :height height
		     :min-width width
		     :min-height height
		     :internal-border-width 1
		     :internal-border-color "#00ff00"
		     :override-parameters '((cursor-type . t))
		     :accept-focus t))
	      ;; Blink cursor
	      (with-current-buffer buffer
		(save-excursion
		  (vterm-clear t))
		(setq-local cursor-type 'box))
	      ;; Focus the child frame
	      (select-frame-set-input-focus vterm-posframe--frame)))))))


  (use-package exec-path-from-shell
    :config
    (when (memq window-system '(mac ns x))
      (exec-path-from-shell-initialize)))


  (use-package shell-pop
    :init
    (custom-set-variables
     '(shell-pop-shell-type (quote ("ansi-term" "*ansi-term*" (lambda nil (ansi-term shell-pop-term-shell)))))
     '(shell-pop-window-size 30)
     '(shell-pop-full-span t)
     '(shell-pop-window-position "bottom")
     '(shell-pop-autocd-to-working-dir t)
     '(shell-pop-restore-window-configuration t)
     '(shell-pop-cleanup-buffer-at-process-exit t)))
#+END_SRC
** which-key
#+BEGIN_SRC emacs-lisp
  (use-package which-key
    :config
    (setq which-key-idle-delay 0.5)
    (which-key-mode))
#+END_SRC
** yasnippet
#+BEGIN_SRC emacs-lisp
  (use-package yasnippet
    :config
    (yas-reload-all)
    (add-hook 'prog-mode-hook #'yas-minor-mode))

  (use-package yasnippet-snippets)
#+END_SRC
** youdao-dictionary
#+BEGIN_SRC emacs-lisp
  (use-package youdao-dictionary)
#+END_SRC

* Configs
#+BEGIN_SRC emacs-lisp
  ;;custom file
  ;;(setq custom-file (expand-file-name "~/.emacs.d/custom.el" user-emacs-directory))
  ;;(load-file custom-file)

  ;;ido mode
  ;;(setq indo-enable-flex-matching t)
  ;;(setq ido-everywhere t)
  ;;(ido-mode t)

  ;;diable error tone
  (setq ring-bell-function 'ignore)

  ;;no backup file
  (setq make-backup-files nil)
  (setq auto-save-default nil)

  ;;show recent file
  (recentf-mode 1)
  (setq recentf-max-menu-items 15)

  ;;delete selection
  (delete-selection-mode 1)

  ;;paste from clipboard
  (setq x-select-enable-clipboard t)

  ;;replace Yes/No with y/n
  (fset 'yes-or-no-p 'y-or-n-p)

  ;;exec-path
  (add-to-list 'exec-path "/usr/local/bin")

  ;;emacs deamon
  (unless (server-running-p) (server-start))

  ;;tab-width
  (setq tab-width 4)

#+END_SRC

* UI
#+BEGIN_SRC emacs-lisp
  ;;theme
  (use-package dracula-theme
    :init
    (load-theme 'dracula t)
    (set-cursor-color "#00ff00"))

  (use-package all-the-icons)
  (use-package doom-modeline
    :after (all-the-icons)
    :init (doom-modeline-mode 1)
    :config
    (setq doom-modeline-major-mode-icon nil)
    (setq doom-modeline-height 1)
    (set-face-attribute 'mode-line nil :family "Source Code Pro" :height 150)
    (set-face-attribute 'mode-line-inactive nil :family "Source Code Pro" :height 150))

  ;; set transparency
  (set-frame-parameter (selected-frame) 'alpha '(90 90))
  (add-to-list 'default-frame-alist '(alpha 90 90))

  ;; display time
  (display-time-mode 1)
  (setq display-time-24hr-format t)
  (setq display-time-day-and-date t)

  ;; display battery
  (display-battery-mode 1)

  ;; (require 'nano)
  ;; (require 'nano-theme-dark)

  ;;font
  (add-to-list 'default-frame-alist `(font . ,my-font))
  (unless is-on-linux
	(set-fontset-font t 'symbol (font-spec :family "Apple Color Emoji") nil 'prepend))

  ;;hide tool bar
  (tool-bar-mode -1)

  ;;hide scroll bar
  (scroll-bar-mode -1)

  ;;hide menu bar
  ;; (unless (display-graphic-p)
  ;;   (menu-bar-mode -1))
  (menu-bar-mode -1)

  ;;show line number
  (global-linum-mode t)

  ;;disable welcome page
  (setq inhibit-splash-screen t)

  ;;default open with full screen
  (setq initial-frame-alist (quote ((fullscreen . maximized))))

  ;;set cursor type
  (setq-default cursor-type 'box)
  (set-cursor-color "#00ff00")
  (blink-cursor-mode 0)

  ;;show match ()
  (add-hook 'emacs-lisp-mode-hook 'show-paren-mode)

  ;;highlight current line
  (when (display-graphic-p)
	(global-hl-line-mode))

  (setq visible-bell nil)

  ;;Display lambda as λ
  (global-prettify-symbols-mode 1)
  (setq prettify-symbols-alist '(("lambda" . 955)))

#+END_SRC
* Keybindings
** general
#+BEGIN_SRC emacs-lisp
  (general-create-definer my-leader-def
    :states '(normal insert visual emacs)
    :keymaps 'override
    :prefix my-leader-key
    :non-normal-prefix "C-,")

  (general-define-key
   :states '(normal visual)
   "gl" 'evil-avy-goto-line
   ";" 'ivy-switch-buffer
   "," 'evil-switch-to-windows-last-buffer
   "." 'evil-avy-goto-char-timer
   "g;" 'repeat-find-char
   "g," 'repeat-find-char-reverse
   "g." 'evil-repeat)

  (general-define-key
   "<f5>" 'revert-buffer
   "C-;" 'vterm-posframe-toggle
   "C-s" 'swiper
   "M-y" 'counsel-yank-pop
   "M-RET" 'lsp-execute-code-action

   "C-." 'company-files)
#+END_SRC
** leader-keys
*** a-key
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    "<SPC>" 'counsel-M-x
    "q" '((lambda ()
	    (interactive)
	    (progn
	      (kill-current-buffer)
	      (when (> (length (window-list)) 1)
		(delete-window))))
	  :wk "kill-buffer")
    "'" 'shell-pop)
#+END_SRC
*** buffer
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    "b" '(:wk "buffer")

    "b+" 'er/expand-region
    "bb" 'bufler
    "bs" 'counsel-switch-buffer-other-window
    "bS" '((lambda ()
	     "create a new scratch buffer to work in. (could be *scratch* - *scratchX*)"
	     (interactive)
	     (let ((n 0)
		   bufname)
	       (while (progn
			(setq bufname (concat "*scratch"
					      (if (= n 0) "" (int-to-string n))
					      "*"))
			(setq n (1+ n))
			(get-buffer bufname)))
	       (switch-to-buffer (get-buffer-create bufname))
	       (if (= n 1) (lisp-interaction-mode))))
	   :wk "new scratch"))
#+END_SRC
*** commenter
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    "c" '(:wk "commenter")

    "cc" 'evilnc-comment-or-uncomment-lines
    "cp" 'evilnc-copy-and-comment-lines
    "cb" 'evilnc-comment-or-uncomment-paragraphs)
#+END_SRC
*** file
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    "f" '(:wk "file")

    "fe" '((lambda () (interactive) (find-file "~/.emacs.d/myinit.org"))
	   :wk "open config")
    "fE" '((lambda () (interactive) (org-babel-load-file (expand-file-name "~/.emacs.d/myinit.org")))
	   :wk "reload config")
    "ff" 'counsel-find-file
    "fF" '((lambda ()
	     (interactive)
	     (shell-command "open -R ."))
	   :wk "open in Finder")
    "fr" 'counsel-recentf
    "fR" 'revert-buffer
    "fd" 'dired
    "fs" 'save-buffer
    "fS" 'save-some-buffers
    "fp" '(lambda () (interactive) (when (file-exists-p (current-kill 0))
				(find-file (current-kill 0)))))
#+END_SRC
*** git
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    "g" '(:wk "git")

    "gg" 'magit-status
    "gd" 'hydra-diff-hl/body
    "gx" 'diff-hl-revert-hunk
    "gm" 'git-messenger:popup-message)
#+END_SRC
*** jump
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    "j" '(:wk "jump")

    "jj" 'avy-goto-char-2
    "jJ" 'avy-goto-char
    "jt" 'avy-goto-char-timer
    "jw" 'avy-goto-word-1
    "jl" 'avy-goto-line)
#+END_SRC
*** music
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    ;; music
    "m" '(:wk "music")
    "mM" '(hydra-bongo/body :wk "music")
    "mm" '(init-goto-bongo :wk "goto music")
    "m <SPC>" 'bongo-pause/resume
    "ms" 'bongo-pause/resume
    "mr" 'bongo-play-random
    "mn" 'bongo-play-next
    "mp" 'bongo-play-previous
    "mf" 'bongo-seek-forward-10
    "mF" 'bongo-seek-forward-60
    "mb" 'bongo-seek-backward-10
    "mB" 'bongo-seek-backward-60)
#+END_SRC
*** note
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    "n" '(:wk "note")
    "nn" 'org-capture
    "nf" '((lambda () (interactive) (find-file my-note-file))
	   :wk "open note"))
#+END_SRC
*** project
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    "p" '(:wk "project")

    "pp" 'projectile-command-map
    "pt" '(projectile-run-vterm
	   :wk "project term"))
#+END_SRC
*** quit
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    "<ESC>" '(:wk "quit")
    "<ESC> <ESC>" 'save-buffers-kill-terminal
    "<ESC> 1" 'restart-emacs)
#+END_SRC
*** search
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    "s" '(:wk "search")

    "si" 'counsel-imenu
    "sr" 'counsel-rg
    "sf" 'find-file-in-project
    "sF" 'counsel-fzf
    "sL" 'counsel-locate
    "ss" 'swiper-thing-at-point
    "sS" 'swiper-all
    "sg" 'google-this
    "sd" '(lambda () (interactive)
	    (if (display-graphic-p)
		(youdao-dictionary-search-at-point-posframe)
	      (youdao-dictionary-search-at-point+)))
    "sl" 'browse-url)
#+END_SRC
*** terminal
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    ;; terminal
    "t" '(:wk "terminal")
    "tt" '((lambda ()
	     (interactive) (my-new-term))
	   :wk "new terminal"))
#+END_SRC
*** toggle
#+BEGIN_SRC emacs-lisp
  (my-leader-def
    "T" '(:wk "toggle")

    "Te" 'toggle-company-english-helper

    "Tt" '((lambda ()
	     (interactive)
	     (let ((alpha (frame-parameter nil 'alpha)))
	       (set-frame-parameter
		nil 'alpha
		(if (eql (cond ((numberp alpha) alpha)
			       ((numberp (cdr alpha)) (cdr alpha))
			       ;; Also handle undocumented (<active> <inactive>) form.
			       ((numberp (cadr alpha)) (cadr alpha)))
			 100)
		    '(85 . 50) '(100 . 100)))))
	   :wk "toggle-transparency"))
#+END_SRC
*** window
#+BEGIN_SRC emacs-lisp
  (defhydra hydra-window ()
    "window"
    ("h" windmove-left)
    ("j" windmove-down)
    ("k" windmove-up)
    ("l" windmove-right)
    ("H" windmove-swap-states-left)
    ("J" windmove-swap-states-down)
    ("K" windmove-swap-states-up)
    ("L" windmove-swap-states-right)
    ("C-h" evil-window-move-far-left)
    ("C-j" evil-window-move-very-bottom)
    ("C-k" evil-window-move-very-top)
    ("C-l" evil-window-move-far-right)
    ("/" (lambda ()
	   (interactive)
	   (split-window-right)
	   (windmove-right))
     "v-split")
    ("?" (lambda ()
	   (interactive)
	   (split-window-below)
	   (windmove-down))
     "h-split")
    ("0" balance-windows "balance-windows")
    ("=" enlarge-window "enlarge-v")
    ("-" shrink-window "shrink-v")
    ("<" shrink-window-horizontally "shrink-h")
    (">" enlarge-window-horizontally "enlarge-h")
    ("g" ace-window "goto")
    ("s" ace-swap-window "swap")
    ("x" delete-window "x")
    ("d" ace-delete-window "del")
    ("m" delete-other-windows "maximize" :color blue)
    ("q" nil "cancel"))

  (my-leader-def
    "w" '(:wk "window")

    "ww" 'hydra-window/body
    "wt" 'awesome-fast-switch/body
    "wh" 'windmove-left
    "wj" 'windmove-down
    "wk" 'windmove-up
    "wl" 'windmove-right
    "wH" 'windmove-swap-states-left
    "wJ" 'windmove-swap-states-down
    "wK" 'windmove-swap-states-up
    "wL" 'windmove-swap-states-right
    "wg" 'ace-window
    "ws" 'ace-swap-window
    "w/" 'split-window-right
    "w?" 'split-window-below
    "wm" 'delete-other-windows
    "wd" 'delete-window)
#+END_SRC

** major-mode-keys
*** bufler-list-mode
#+BEGIN_SRC emacs-lisp
  (general-define-key
   :states 'normal
   :keymaps 'bufler-list-mode-map
   "r" 'bufler-list
   "q" '(lambda ()
	  (interactive)
	  (progn
	    (kill-current-buffer)
	    (when (> (length (window-list)) 1)
	      (delete-window))))
   "d" '(lambda ()
	  (interactive)
	  (when
	      (yes-or-no-p "kill buffer?")
	    (bufler-list-buffer-kill)))
   "s" 'bufler-list-buffer-save
   "RET" 'bufler-list-buffer-switch)
#+END_SRC
*** bongo-playlist-mode
#+BEGIN_SRC emacs-lisp
  (general-define-key
   :states 'normal
   :keymaps 'bongo-playlist-mode-map
   "RET" 'bongo-play
   "TAB" 'bongo-toggle-collapsed
   "J" 'bongo-next-header-line
   "K" 'bongo-previous-header-line
   "r" 'bongo-play-random
   "c" 'bongo-recenter
   "s" 'bongo-pause/resume
   "f" 'bongo-seek-forward-10
   "F" 'bongo-seek-forward-60
   "b" 'bongo-seek-backward-10
   "B" 'bongo-seek-backward-60
   "q" 'bongo-quit
   "Q" 'bongo-stop)
#+END_SRC
*** markdown-mode
#+BEGIN_SRC emacs-lisp
  (general-define-key
   :states 'normal
   :prefix my-leader-key
   :keymaps 'markdown-mode-map
   "l" '(:wk "md")
   "lp" 'markdown-live-preview-mode
   "lr" '((lambda ()
	    (interactive)
	    (shell-command
	     (format "open %s"
		     (shell-quote-argument (buffer-file-name)))))
	  :wk "open"))
#+END_SRC
*** python-mode
#+BEGIN_SRC emacs-lisp
  (general-define-key
   :states 'normal
   :prefix my-leader-key
   :keymaps 'python-mode-map
   "l" '(:wk "python")
   "lf" 'lsp-format-buffer
   "lr" '(lsp-rename :wk "rename")
   "ld" 'lsp-find-definition)
#+END_SRC
*** org-mode
#+BEGIN_SRC emacs-lisp
  (general-define-key
   :states 'normal
   :prefix my-leader-key
   :keymaps 'org-mode-map
   "l" '(:wk "org")
   "lp" '(grip-mode :wk "preview")
   "ll" 'org-babel-remove-result
   "lr" 'org-ctrl-c-ctrl-c
   "lt" 'org-insert-structure-template)
#+END_SRC
*** term-mode
#+begin_src emacs-lisp
  (general-define-key
   :states 'normal
   :keymaps 'term-mode-map
   "q" '(term-interrupt-subjob
		 :wd "quit"))
#+end_src
